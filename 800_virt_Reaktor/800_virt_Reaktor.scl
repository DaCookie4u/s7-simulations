FUNCTION_BLOCK "800_virt_Reaktor"
{ S7_Optimized_Access := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      PAA_AB0 : Byte;
   END_VAR

   VAR_OUTPUT 
      PAE_EB0 : Byte;
      PAE_EW64 : Int;
   END_VAR

   VAR 
      DI : Struct
         S1 : Bool;
         B1 : Bool;
         B2 : Bool;
         B3 : Bool;
         B4 : Bool;
         S2 : Bool;
         S3 : Bool;
         RES7 : Bool;
      END_STRUCT;
      AI_B5 : Int;
      DQ : Struct
         Y1 : Bool;
         Y2 : Bool;
         Y3 : Bool;
         H : Bool;
         M : Bool;
         RES05 : Bool;
         RES06 : Bool;
         RES07 : Bool;
      END_STRUCT;
      Reset_Simulation : Bool := true;
      Pause_Simulation : Bool;
      Fast_Forward : Bool;
      Fuellstand : Real;
      Temperatur : Real;
      Abtastzyklus {OriginalPartName := 'IEC_TIMER'; LibVersion := '1.0'} : TON_TIME;
      Speicher_Abtastflanke : Bool;
   END_VAR

   VAR_TEMP 
      Abtastzeit : Time;
      Fuellstand_kP : Real;
      Heizung_n : Real;
   END_VAR

   VAR CONSTANT 
      Niveau_1 : Int := 40;
      Niveau_2 : Int := 75;
      Temperatur_Min : Int := 20;
      Temperatur_Max : Int := 60;
      Temperatur_S5_An : Int := 45;
      Temperatur_S5_Aus : Int := 44;
   END_VAR


BEGIN
	(*
	  Simulation eines Reaktors
	  
	  Ansteuerbare Aktuatoren, ausgehend von %AB0 am Eingang #PAA_AB0:
	  %A0.0   Y1      Zulaufventil 1
	  %A0.1   Y2      Zulaufventil 2
	  %A0.3   Y3      Ablassventil
	  %A0.4   Y4      Heizung
	  %A0.5   Y5      Rührmotor
	  
	  Simulierte digitale Sensoren und Taster, ausgehend von %EB0 am Ausgang #PAE_EB0:
	  %E0.0   S1      Taster S1 (NO)
	  %E0.1   B1      Leermeldung (NO, meldet TRUE bei leerem Tank)
	  %E0.2   B2      Niveaumeldung 1 (NO)
	  %E0.3   B3      Niveaumeldung 2 (NO)
	  %E0.4   B4      Temperaturmeldung (NO, meldet TRUE bei Solltemperatur)
	  %E0.5   S2      Taster S2 (NO)
	  %E0.6   S3      Taster S3 (NC)
	  
	  Simulierte analoge Sensoren, ausgehend von %EW64 am Ausgang #PAE_EW64:
	  %EW64   B1      Füllstandssensor (0..10V)
	*)
	
	// Initialisierung der Simulation
	IF #Reset_Simulation THEN
	    #DI.S1 := FALSE;
	    #DI.S2 := FALSE;
	    #DI.S3 := FALSE;
	    
	    #Fuellstand := 0;
	    #Temperatur := 0;
	END_IF;
	#Reset_Simulation := FALSE;
	
	// Abtastzyklus und dessen Flanke erzeugen
	IF #Fast_Forward THEN
	    #Abtastzeit := T#50ms;
	ELSE
	    #Abtastzeit := T#100ms;
	END_IF;
	#Abtastzyklus(IN := NOT #Speicher_Abtastflanke,
	              PT := #Abtastzeit,
	              Q => #Speicher_Abtastflanke);
	
	IF NOT #Speicher_Abtastflanke OR #Pause_Simulation THEN
	    RETURN;
	END_IF;
	
	// Übernahme des PAA
	#DQ.Y1 := (#PAA_AB0 AND 16#01) > 0;
	#DQ.Y2 := (#PAA_AB0 AND 16#02) > 0;
	#DQ.Y3 := (#PAA_AB0 AND 16#04) > 0;
	#DQ.H := (#PAA_AB0 AND 16#08) > 0;
	#DQ.M := (#PAA_AB0 AND 16#10) > 0;
	
	// Behälter füllen bzw. entleeren
	IF #DQ.Y1 OR #DQ.Y2 OR #DQ.Y3 THEN
	    #Fuellstand_kP := BOOL_TO_INT(#DQ.Y1) * 500 + BOOL_TO_INT(#DQ.Y2) * 500 - BOOL_TO_INT(#DQ.Y3) * 500;
	    #Fuellstand := #Fuellstand + #Fuellstand_kP / 1000;
	    #Fuellstand := LIMIT(IN := #Fuellstand, MN := 0, MX := 100);
	END_IF;
	
	// Temperatur berechnen
	IF #DQ.H THEN
	    #Heizung_n := 3 * (#Fuellstand + 1);
	    #Temperatur := ((#Temperatur_Max - #Temperatur) / #Heizung_n) + #Temperatur;
	ELSIF NOT #DQ.H THEN
	    #Heizung_n := #Fuellstand ** 2 + 1;
	    #Temperatur := ((#Temperatur_Min - #Temperatur) / #Heizung_n) + #Temperatur;
	END_IF;
	#Temperatur := LIMIT(IN := #Temperatur, MN := #Temperatur_Min, MX := #Temperatur_Max);
	
	// Sensoren auswerten
	#AI_B5 := SCALE_X_INT(MIN := 0, MAX := 27648, VALUE := NORM_X_REAL(MIN := 0, MAX := 100, VALUE := #Fuellstand));
	#DI.B1 := (#Fuellstand > 0);
	#DI.B2 := (#Fuellstand >= #Niveau_1);
	#DI.B3 := (#Fuellstand >= #Niveau_2);
	#DI.B4 := (#Temperatur >= #Temperatur_S5_An AND NOT #DI.B4) OR (#Temperatur >= #Temperatur_S5_Aus AND #DI.B4);
	
	// PAE ausgeben
	#PAE_EB0 := 16#0 OR BOOL_TO_BYTE(#DI.S1);
	#PAE_EB0 := #PAE_EB0 OR SHL(IN := BOOL_TO_BYTE(#DI.B1), N := 1);
	#PAE_EB0 := #PAE_EB0 OR SHL(IN := BOOL_TO_BYTE(#DI.B2), N := 2);
	#PAE_EB0 := #PAE_EB0 OR SHL(IN := BOOL_TO_BYTE(#DI.B3), N := 3);
	#PAE_EB0 := #PAE_EB0 OR SHL(IN := BOOL_TO_BYTE(#DI.B4), N := 4);
	#PAE_EB0 := #PAE_EB0 OR SHL(IN := BOOL_TO_BYTE(#DI.S2), N := 5);
	#PAE_EB0 := #PAE_EB0 OR SHL(IN := BOOL_TO_BYTE(NOT #DI.S3), N := 6);
	
	#PAE_EW64 := #AI_B5;
END_FUNCTION_BLOCK

